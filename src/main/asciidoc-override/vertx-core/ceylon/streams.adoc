== Streams

There are several objects in Vert.x that allow items to be read from and written.

In previous versions the streams.adoc package was manipulating `link:../../ceylondoc/vertx-core//buffer/Buffer.type.html[Buffer]`
objects exclusively. From now, streams are not coupled to buffers anymore and they work with any kind of objects.

In Vert.x, write calls return immediately, and writes are queued internally.

It's not hard to see that if you write to an object faster than it can actually write the data to
its underlying resource, then the write queue can grow unbounded - eventually resulting in
memory exhaustion.

To solve this problem a simple flow control (_back-pressure_) capability is provided by some objects in the Vert.x API.

Any flow control aware object that can be _written-to_ implements `link:../../ceylondoc/vertx-core//streams/WriteStream.type.html[WriteStream]`,
while any flow control object that can be _read-from_ is said to implement `link:../../ceylondoc/vertx-core//streams/ReadStream.type.html[ReadStream]`.

Let's take an example where we want to read from a `ReadStream` then write the data to a `WriteStream`.

A very simple example would be reading from a `link:../../ceylondoc/vertx-core//net/NetSocket.type.html[NetSocket]` then writing back to the
same `NetSocket` - since `NetSocket` implements both `ReadStream` and `WriteStream`. Note that this works
between any `ReadStream` and `WriteStream` compliant object, including HTTP requests, HTTP responses,
async files I/O, WebSockets, etc.

A naive way to do this would be to directly take the data that has been read and immediately write it
to the `NetSocket`:

[source,ceylon]
----
value server = vertx.createNetServer(NetServerOptions {
  port = 1234;
  host = "localhost";
});
server.connectHandler((NetSocket sock) {
  sock.handler((Buffer buffer) {
    // Write the data straight back
    sock.write(buffer);
  });
}).listen();

----

There is a problem with the example above: if data is read from the socket faster than it can be
written back to the socket, it will build up in the write queue of the `NetSocket`, eventually
running out of RAM. This might happen, for example if the client at the other end of the socket
wasn't reading fast enough, effectively putting back-pressure on the connection.

Since `NetSocket` implements `WriteStream`, we can check if the `WriteStream` is full before
writing to it:

[source,ceylon]
----
value server = vertx.createNetServer(NetServerOptions {
  port = 1234;
  host = "localhost";
});
server.connectHandler((NetSocket sock) {
  sock.handler((Buffer buffer) {
    if (!sock.writeQueueFull()) {
      sock.write(buffer);
    };
  });

}).listen();

----

This example won't run out of RAM but we'll end up losing data if the write queue gets full. What we
really want to do is pause the `NetSocket` when the write queue is full:

[source,ceylon]
----
value server = vertx.createNetServer(NetServerOptions {
  port = 1234;
  host = "localhost";
});
server.connectHandler((NetSocket sock) {
  sock.handler((Buffer buffer) {
    sock.write(buffer);
    if (sock.writeQueueFull()) {
      sock.pause();
    };
  });
}).listen();

----

We're almost there, but not quite. The `NetSocket` now gets paused when the file is full, but we also need to unpause
it when the write queue has processed its backlog:

[source,ceylon]
----
value server = vertx.createNetServer(NetServerOptions {
  port = 1234;
  host = "localhost";
});
server.connectHandler((NetSocket sock) {
  sock.handler((Buffer buffer) {
    sock.write(buffer);
    if (sock.writeQueueFull()) {
      sock.pause();
      sock.drainHandler(() {
        sock.resume();
      });
    };
  });
}).listen();

----

And there we have it. The `link:../../ceylondoc/vertx-core//streams/WriteStream.type.html#drainHandler((@io.vertx.codegen.annotations.Nullable :: io.vertx.core.Handler))[drainHandler]` event handler will
get called when the write queue is ready to accept more data, this resumes the `NetSocket` that
allows more data to be read.

Wanting to do this is quite common while writing Vert.x applications, so we provide a helper class
called `link:../../ceylondoc/vertx-core//streams/Pump.type.html[Pump]` that does all of this hard work for you.
You just feed it the `ReadStream` plus the `WriteStream` then start it:

[source,ceylon]
----
import io.vertx.ceylon.core.streams { pump } 

...

value server = vertx.createNetServer(NetServerOptions {
  port = 1234;
  host = "localhost";
});
server.connectHandler((NetSocket sock) {
  pump.pump(sock, sock).start();
}).listen();

----

This does exactly the same thing as the more verbose example.

Let's now look at the methods on `ReadStream` and `WriteStream` in more detail:

=== ReadStream

`ReadStream` is implemented by `link:../../ceylondoc/vertx-core//http/HttpClientResponse.type.html[HttpClientResponse]`, `link:../../ceylondoc/vertx-core//datagram/DatagramSocket.type.html[DatagramSocket]`,
`link:../../ceylondoc/vertx-core//http/HttpClientRequest.type.html[HttpClientRequest]`, `link:../../ceylondoc/vertx-core//http/HttpServerFileUpload.type.html[HttpServerFileUpload]`,
`link:../../ceylondoc/vertx-core//http/HttpServerRequest.type.html[HttpServerRequest]`, `link:../../ceylondoc/vertx-core//http/HttpServerRequestStream.type.html[HttpServerRequestStream]`,
`link:../../ceylondoc/vertx-core//eventbus/MessageConsumer.type.html[MessageConsumer]`, `link:../../ceylondoc/vertx-core//net/NetSocket.type.html[NetSocket]`, `link:../../ceylondoc/vertx-core//net/NetSocketStream.type.html[NetSocketStream]`,
`link:../../ceylondoc/vertx-core//http/WebSocket.type.html[WebSocket]`, `link:../../ceylondoc/vertx-core//http/WebSocketStream.type.html[WebSocketStream]`, `link:../../ceylondoc/vertx-core//TimeoutStream.type.html[TimeoutStream]`,
`link:../../ceylondoc/vertx-core//file/AsyncFile.type.html[AsyncFile]`.

Functions:

- `link:../../ceylondoc/vertx-core//streams/ReadStream.type.html#handler((@io.vertx.codegen.annotations.Nullable :: io.vertx.core.Handler))[handler]`:
set a handler which will receive items from the ReadStream.
- `link:../../ceylondoc/vertx-core//streams/ReadStream.type.html#pause()[pause]`:
pause the handler. When paused no items will be received in the handler.
- `link:../../ceylondoc/vertx-core//streams/ReadStream.type.html#resume()[resume]`:
resume the handler. The handler will be called if any item arrives.
- `link:../../ceylondoc/vertx-core//streams/ReadStream.type.html#exceptionHandler(io.vertx.core.Handler)[exceptionHandler]`:
Will be called if an exception occurs on the ReadStream.
- `link:../../ceylondoc/vertx-core//streams/ReadStream.type.html#endHandler((@io.vertx.codegen.annotations.Nullable :: io.vertx.core.Handler))[endHandler]`:
Will be called when end of stream is reached. This might be when EOF is reached if the ReadStream represents a file,
or when end of request is reached if it's an HTTP request, or when the connection is closed if it's a TCP socket.

=== WriteStream

`WriteStream` is implemented by `link:../../ceylondoc/vertx-core//http/HttpClientRequest.type.html[HttpClientRequest]`, `link:../../ceylondoc/vertx-core//http/HttpServerResponse.type.html[HttpServerResponse]`
`link:../../ceylondoc/vertx-core//http/WebSocket.type.html[WebSocket]`, `link:../../ceylondoc/vertx-core//net/NetSocket.type.html[NetSocket]`, `link:../../ceylondoc/vertx-core//file/AsyncFile.type.html[AsyncFile]`,
`link:../../ceylondoc/vertx-core//datagram/PacketWritestream.type.html[PacketWritestream]` and `link:../../ceylondoc/vertx-core//eventbus/MessageProducer.type.html[MessageProducer]`

Functions:

- `link:../../ceylondoc/vertx-core//streams/WriteStream.type.html#write(java.lang.Object)[write]`:
write an object to the WriteStream. This method will never block. Writes are queued internally and asynchronously
written to the underlying resource.
- `link:../../ceylondoc/vertx-core//streams/WriteStream.type.html#setWriteQueueMaxSize(int)[setWriteQueueMaxSize]`:
set the number of object at which the write queue is considered _full_, and the method `link:../../ceylondoc/vertx-core//streams/WriteStream.type.html#writeQueueFull()[writeQueueFull]`
returns `true`. Note that, when the write queue is considered full, if write is called the data will still be accepted
and queued. The actual number depends on the stream implementation, for `link:../../ceylondoc/vertx-core//buffer/Buffer.type.html[Buffer]` the size
represents the actual number of bytes written and not the number of buffers.
- `link:../../ceylondoc/vertx-core//streams/WriteStream.type.html#writeQueueFull()[writeQueueFull]`:
returns `true` if the write queue is considered full.
- `link:../../ceylondoc/vertx-core//streams/WriteStream.type.html#exceptionHandler(io.vertx.core.Handler)[exceptionHandler]`:
Will be called if an exception occurs on the `WriteStream`.
- `link:../../ceylondoc/vertx-core//streams/WriteStream.type.html#drainHandler((@io.vertx.codegen.annotations.Nullable :: io.vertx.core.Handler))[drainHandler]`:
The handler will be called if the `WriteStream` is considered no longer full.

=== Pump

Instances of Pump have the following methods:

- `link:../../ceylondoc/vertx-core//streams/Pump.type.html#start()[start]`:
Start the pump.
- `link:../../ceylondoc/vertx-core//streams/Pump.type.html#stop()[stop]`:
Stops the pump. When the pump starts it is in stopped mode.
- `link:../../ceylondoc/vertx-core//streams/Pump.type.html#setWriteQueueMaxSize(int)[setWriteQueueMaxSize]`:
This has the same meaning as `link:../../ceylondoc/vertx-core//streams/WriteStream.type.html#setWriteQueueMaxSize(int)[setWriteQueueMaxSize]` on the `WriteStream`.

A pump can be started and stopped multiple times.

When a pump is first created it is _not_ started. You need to call the `start()` method to start it.
<a href="mailto:julien@julienviet.com">Julien Viet</a>